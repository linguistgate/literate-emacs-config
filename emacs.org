#+TITLE: Emacs Literate Configuration
#+AUTHOR: Matthew Edward Adams 
#+PROPERTY: header-args :tangle yes
#+STARTUP: indent
* Design Concerns
** Why a Literate Configuration?

Literate programming is human-centric in the sense that programming code is "tangled" with prose. Moreover, the "program" unfolds in a way that reflects human reasoning, not machine logic. I don't have a stake in whether I think literate programming is good (and I don't even really know where it stands among programmers and coders in 2018). In wanting my Emacs configuration to be configurable but not get out of hand, I have been trying different options over the years. The literate programming examples I've seen online have made a good case for something resembling "literate" programming as the basis for a dot Emacs configuration.

** Must-Haves

- A reliable and fast-loading configuration; a byte-compiled =init.elc= for even faster load time
- A writing-centric setup with a beautiful writing theme (=poet-theme=)
- A clear way of handling images that are associated with Org files
- A general philosophy of avoiding new packages; favor taking the relevant bits of code and pasting them into this configuraiton with literate explanations of what the code does
- A working, reliable, and consistent tagging, capture, and refiling system based on principles of Building a Second Brain, PARA, and other ideas from Tiago Forte
- A clear and consistent structure to my =~/org= directory that is recapitulated in my capture and refiling system
- A dashboard to replace =organizer.org= or to heavily supplement it
- Spellchecking, hydras, and other navigation tools that favor /writing/ and /editing/
- An =.emacs.d/= directory that properly files away user configuration and is not cluttered
- =init.el= and associated files that make sense, aren't overly complicated, and don't contain code I don't use
- A way to distinguish packages on MELPA, packages not there, and packages that are experimental, and a way to sandbox non-working packages
- A way to load a minimally working config that will always have my bare bones

*************** DONE Create working minimal config   :@digital:
*************** END
*************** TODO Decide on how to handle picture handling across Areas, Projects, and Resources :@digital:
*************** END
*************** DONE Make Projectile work better with Projects :@digital:
*************** END

** Things That Will Continue to Be Built Out

- A server and publishing system that allows for Wikis, notebooks, and web pages to be created from Org content
- Various ways of capturing online material and storing it in =resources/= for offline access (=org-download=, etc.)
- Good langauge and font support for German, Korean, and French when I want to type in and use those languages
- Non-linear, non-hierarchical tools like =org-brain= as ways to enhance the knowledge management system I am building

** Desirables
- Ledger ([[https://www.ledger-cli.org/][Ledger]]). Also possibly [[https://github.com/NicolasPetton/elbank][Elbank]].
- [[https://github.com/abo-abo/org-download][Org-download]]
- Possibly some code from [[https://github.com/rnkn/fountain-mode/][Founntain Mode]] (screenwriting)
- Check out [[https://www.gnu.org/software/emacs-muse/manual/muse.html][Muse]] alongside Org pubishing system. What are my needs?
- Possible integration of [[https://github.com/novoid/Memacs][Memacs]] someday. 

** Explanation of Captures, CPB, Daybook, Hub

The =captures= and =cpb= directories encapsulate [[https://medium.com/@tasshin/implementing-a-second-brain-in-emacs-and-org-mode-ef0e44fb7ca5][progressive summarization]], at least the first four layers:

- Layer 1 :: When you encounter something interesting, capture it
- Layer 2 :: Bold the most interesting parts
- Layer 3 :: Highlight the most interesting bolded sections
- Layer 4 :: Summarize the bolded portions and the note in your own words
- Layer 5 :: Turn your notes into something new: a tweet, a blog post, even a book

The Commonplace Book directory, or =cpb= direcotry, includes my journals and notebooks, all of which aim to have a Coleridgean flair. Captures pulls from the Web (using Firefox’s =org-capture= extension, primarily). Items not captured via this method can be entered manually with a capture template, or in certain cases can be put into =unfiled= for later assimilation.

The =daybook= allows for bullet-journal carryover of tasks and includes short notes about what I accomplished each day. The Agenda pulls heavily from the daybook. It is intended to be a serious commitment and is separate from, but can often influence or engage with, projects and notes.

The =hub= replaces several attempts at realzing “Areas” wtihin Tiago Forte’s P.A.R.A. framework. I’ve gone through highly articulated structures that list out all ongoing spheres of activity. Under the PARA rubric, areas are "[[https://medium.com/@tasshin/implementing-a-second-brain-in-emacs-and-org-mode-ef0e44fb7ca5][ongoing spheres of activity]]." They are /not/ projects, but projects often emerge from these areas. They are also /not/ resources, which are supposed to be "materials not tied to a specific project or area." My solution is create a large Areas-esque file, =systems.org=, that has subheadings devoted to Home, Digital, Transportation, etc., which are my ongoing spheres of activity. Headings within these areas can point to projects, instantiate recurring tasks, and so on. The =calendar.org= lists out yearly anniversaries I don’t want to forget, and =contacts.xxx= is a file that either will use =org-contacts= or revert to the powerful BBDB system. The final file =meta.org= has code that tracks tags throughout the =org= sytem.

The thought here is that these four directories directly interact with the Agenda in certain ways or represent hard concerns of P.A.R.A.-based workflow. I’ve moved a bit away from GTD, except for contextual tagging and ticklers (setting tasks way into the future to remind me of something I want to do).

Within my re-imagined home directory, the =library= sits /outside of/ the =org= directory proper, as it should. There are two reasons for this:

1. I am moving to a server framework in which the “library” in its entirety sits on a private server, and local computers check out relevant portions of the library.
2. The =org= directory is not synonymous with /all/ work I do or my /entire/ knowledge management system. The =org= directory reassumes real meaning with fewer files and more sensible subdirectories that directly reflect what I am doing with Org.

In addition, projects (including writing, blogging, coding, curriculum development) are housed outside of the =org= structure. If something project-wise ossifies enough, it should enter the library or library archives, and =cpb=-based work should have been tracking its development all along.

In this way, I’ve recapitulated the P.A.R.A. system (Projects - Areas (Hub) - Resources (Library) - Archives (Library Archives)) in a way that works for my workflow. I feel less burdened by having to update tons of files every day and can return to letting the Agenda tell me what needs to be done and where.

* Org Structure

Currently I am trying the following structure for my Org-based workflow:

#+BEGIN_EXAMPLE
├── captures
│   ├── captures.org
│   └── m.screenshots
├── cpb
│   ├── digital.nb.org
│   ├── journal.org
│   ├── math.nb.org
│   └── reference.org
├── daybook
│   ├── 20190203
│   └── 20190204
└── hub
    ├── calendar.org
    ├── contacts.bbdb
    ├── meta.org
    └── systems.org
#+END_EXAMPLE

This structure represents about a year of thinking through task, workflow, and knowledge management systems. I don’t think it’s my final word, but it does reflect best where I want to lay my energies. I want things simple.

* Configuration
:PROPERTIES:
:VISIBILITY: children
:END:

** Table of Contents :TOC_5_gh:
- [[#design-concerns][Design Concerns]]
  - [[#why-a-literate-configuration][Why a Literate Configuration?]]
  - [[#must-haves][Must-Haves]]
  - [[#things-that-will-continue-to-be-built-out][Things That Will Continue to Be Built Out]]
  - [[#desirables][Desirables]]
  - [[#explanation-of-captures-cpb-daybook-hub][Explanation of Captures, CPB, Daybook, Hub]]
- [[#org-structure][Org Structure]]
- [[#configuration][Configuration]]
  - [[#about-this-file][About This File]]
  - [[#config-file-tweaks][Config File Tweaks]]
    - [[#automatically-tangle][Automatically Tangle]]
    - [[#reload-configuation][Reload Configuation]]
    - [[#visibility-settings][Visibility Settings]]
    - [[#table-of-contents][Table of Contents]]
  - [[#personal-information][Personal Information]]
  - [[#emacs-initialization][Emacs Initialization]]
    - [[#garbage-collection][Garbage Collection]]
    - [[#byte-compilation][Byte Compilation]]
    - [[#startup-behavior][Startup Behavior]]
    - [[#directory-structure][Directory Structure]]
    - [[#package-management][Package Management]]
      - [[#package-settings][Package Settings]]
      - [[#use-package-settings][Use-Package Settings]]
      - [[#manually-set-load-path][Manually Set Load Path]]
      - [[#initialize-package-management][Initialize Package Management]]
  - [[#global-defaults][Global Defaults]]
    - [[#aggressive-indent][Aggressive Indent]]
  - [[#fonts][Fonts]]
    - [[#font-locking][Font Locking]]
    - [[#default-fonts][Default Fonts]]
    - [[#font-switching][Font Switching]]
  - [[#themes][Themes]]
    - [[#themes-in-rotation][Themes in Rotation]]
      - [[#flatui-dark-theme][flatui-dark-theme]]
      - [[#doom-themes][doom-themes]]
      - [[#kaolin-themes][kaolin-themes]]
      - [[#poet-themes][poet-themes]]
    - [[#functions-to-switch-among-themes][Functions to Switch Among Themes]]
  - [[#accessibility][Accessibility]]
  - [[#navigation][Navigation]]
  - [[#project-management][Project Management]]
    - [[#version-control][Version Control]]
      - [[#magit][Magit]]
  - [[#org-mode-initialization][Org-Mode Initialization]]
    - [[#org-modules][Org Modules]]
  - [[#org-agenda][Org Agenda]]
  - [[#org-journal-as-task-daybook][Org Journal as Task Daybook]]
  - [[#org-capture-and-refile][Org Capture and Refile]]
    - [[#capture-code-org-capture][Capture Code (=org-capture=)]]
    - [[#setting-some-refile-behaviors][Setting some Refile behaviors]]
    - [[#refile-targets-integration-with-para][Refile targets: Integration with P.A.R.A.]]
  - [[#clock][Clock]]
  - [[#contacts-management][Contacts Management]]
  - [[#encryption][Encryption]]
  - [[#programming-languages-management][Programming Languages Management]]
  - [[#reference-and-resource-management][Reference and Resource Management]]
  - [[#publishing][Publishing]]
  - [[#writing-environment][Writing Environment]]
    - [[#distraction-free-writing][Distraction-Free Writing]]
  - [[#dashboard][Dashboard]]
  - [[#toc-org][Toc-org]]
  - [[#post-initialization][Post Initialization]]

** About This File

This is a rebuild and refactoring of my Emacs configuration using literate programming techniques.

All of my configuration is stored here (no requiring and providing of subfiles) at present.

It contains some optimization for byte-compiling to ensure a faster load time.

** Config File Tweaks

There are a few tweaks included in this org file that make it a little easier to
work with.

*** Automatically Tangle

First there is a property defined on the file:

#+begin_src :tangle no
header-args :tangle yes
#+end_src

This tells emacs to automatically tangle (include) all code blocks in this file when
generating the code for the config, unless the code block explicitly includes
=:tangle no= as the above code block does.

*** Reload Configuation

Reload my configuration from inside of =org-mode= and assign custom function to =C-c r=.

#+begin_src emacs-lisp
  (defun owl/config-reload ()
    "Reloads ~/.emacs.d/emacs.org at runtime."
    (interactive)
    (org-babel-load-file (expand-file-name "~/.emacs.d/emacs.org")))

  (global-set-key (kbd "C-c r") 'owl/config-reload)
#+end_src

*** Visibility Settings

Next we have a property defined on the [[Configuration][Configuration]] heading that defines the visibility
that tells org to show its direct children on startup. This way a clean outline of all
sub headings under Configuration is shown each time this file is opened in org-mode.

*** Table of Contents

Finally, there is a [[Table of Contents][Table of Contents]] heading that includes the tag: =:TOC_5_gh:=. This
tells an org-mode package =toc-org= to generate a table of contents under this heading
that has a max depth of 5 and is created using Github-style hrefs. This table of contents
is updated everytime the file is saved and makes for a functional table of contents that
works property directly on github.

** Personal Information

Some basic values:

#+begin_src emacs-lisp
  (setq user-full-name "Matthew Edward Adams"
        user-mail-address "m2eadams@gmail.com")
#+end_src

** Emacs Initialization

*** Garbage Collection

I increase the =gc-cons-threshold= to a very high number to decrease the load and compile time.
I'll lower this value significantly after initialization has completed. I don't want to keep this value
too high or it will result in long GC pauses during normal usage.

#+begin_src emacs-lisp
  (eval-and-compile
    (setq gc-cons-threshold 402653184
	  gc-cons-percentage 0.6))
#+end_src

*** Byte Compilation

Disable certain byte compiler warnings to cut down on the noise. This can be removed
if I would like to see any and all byte compiler warnings.

#+begin_src emacs-lisp
  (setq byte-compile-warnings '(not free-vars unresolved noruntime lexical make-local))
#+end_src

*** Startup Behavior

When I startup, I want a minimal Emacs layout: no startup screen, no toolbar, no scrollbar, and no menubar.

#+begin_src emacs-lisp
  (setq inhibit-startup-screen t)
  (tool-bar-mode 0)
  (menu-bar-mode 0)
  (scroll-bar-mode -1)
#+end_src

*** Directory Structure

Set the directory where the Emacs configuration is installed.

#+begin_src emacs-lisp
  (setq user-emacs-directory (file-truename "~/.emacs.d/"))
#+end_src

Customizations are unwanted in =init.el= (and in general), so accidental customizations are stored in =custom.el= under =./user=.

#+begin_src emacs-lisp
  (setq custom-file "~/.emacs.d/user/custom.el")
#+end_src

The default ELPA package directory is =.emacs.d/elpa=. User-defined or non-listed packages should be situated in =./user= directory.

Custom themes should be stored in =./user/themes/=.

*** Package Management

**** Package Settings

Set the =load-path= ourselves and avoid calling =(package-initilize)= (for
performance reasons) so set =package--init-file-ensured= to true to tell =package.el=
to not automatically call it on my behalf. Additionally I'm setting
=package-enable-at-startup= to nil so that packages will not automatically be loaded for me since
=use-package= will be handling that.

#+begin_src emacs-lisp
  (eval-and-compile
    (setq load-prefer-newer t
	  package-user-dir "~/.emacs.d/elpa/"
	  package--init-file-ensured t
	  package-enable-at-startup t)

    (unless (file-directory-p package-user-dir)
      (make-directory package-user-dir t)))
#+end_src

**** Use-Package Settings

Tell =use-package= to always defer loading packages unless explicitly told otherwise. This speeds up
initialization significantly as many packages are only loaded later when they are explicitly used.

#+begin_src emacs-lisp
  (setq use-package-always-defer nil
	use-package-verbose t)
#+end_src

**** Manually Set Load Path

We're going to set the load path ourselves so that we don't have to call =package-initialize= at runtime and incur a large performance hit. This load-path will actually be faster than the one created by =package-initialize= because it appends the elpa packages to the end of the load path. Otherwise any time a builtin package was required it would have to search all of third party paths first.

#+begin_src emacs-lisp
  (eval-and-compile
    (setq load-path (append load-path (directory-files package-user-dir t "^[^.]" t))))
#+end_src

**** Initialize Package Management

Next we are going to require =package.el= and add our additional package archives, 'melpa' and 'org'. Afterwards we need to initialize our packages and then ensure that =use-package= is installed, which we promptly install if it's missing. Finally we load =use-package= and tell it to always install any missing packages.

Note that this entire block is wrapped in =eval-when-compile=. The effect of this is to perform all of the package initialization during compilation so that when byte compiled, all of this time consuming code is skipped. This can be done because the result of byte compiling =use-package= statements results in the macro being fully expanded at which point =use-package= isn't actually required any longer.

Since the code is automatically compiled during runtime, if the configuration hasn't already been previously compiled manually then all of the package initialization will still take place at startup.

#+begin_src emacs-lisp
  (eval-when-compile
    (require 'package)

    (unless (assoc-default "melpa" package-archives)
      (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t))
    (unless (assoc-default "org" package-archives)
      (add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/") t))

    (package-initialize)
    (unless (package-installed-p 'use-package)
      (package-refresh-contents)
      (package-install 'use-package))
    (require 'use-package)
    (setq use-package-always-ensure t))
  (require 'bind-key) ; Needs to be here for :bind to work with byte-compiled emacs.el ... not sure why
#+end_src

***** COMMENT For some reason, =require '(bind-key)= needs to be overtly stated (and seemingly outside of =eval-when-compile= for =:bind= personal keybindings to work with byte compilation (MEA - [2018-11-03 Sat])

** Global Defaults

Some core editing desirables:

- The variable =truncate-lines= turns off wrapping. Long lines will show a continuation character in the right margin.
- To indicate the presence of empty lines at the end of a file, a marker is placed in the left fringe.
- I require final newlines globally.

#+begin_src emacs-lisp
  (setq-default truncate-lines t
                indicate-empty-lines t
                require-final-newline t)
#+end_src

*** Aggressive Indent

The package reindents code after every change, making it purportedly more reliable than =electric-indent-mode=.

Because I mainly use Lisp and Python, I want aggressive indenting to be specific to =emacs-lisp-mode= so I activate it via hook.

#+BEGIN_SRC emacs-lisp
  (use-package aggressive-indent
    :config (add-hook 'emacs-lisp-mode-hook #'aggressive-indent-mode))
#+END_SRC

** Fonts

*** Font Locking

Font Lock mode is a minor mode, always local to a particular buffer, and each buffer's major made tells Font Lock mode which text to fontify (add faces to). A programming language, for instance, will have rules about fontifying syntactically relevant constructs like comments, strings, and function names (from [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Font-Lock.html][the GNU Emacs manual]]).

#+BEGIN_SRC emacs-lisp
  (global-font-lock-mode t)
#+END_SRC

*** Default Fonts

The default fonts need to be installed at least for the relevant user under =~/.fonts=. I currently use the Input series of fonts.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'default-frame-alist '(font . "Input Mono 11" ))
  (set-face-attribute 'default t :font "Input Mono 11" )
  (set-face-attribute 'variable-pitch nil :font "Input Serif 11")
  (set-face-attribute 'fixed-pitch nil :font "Input Mono 11")
  (add-hook 'text-mode-hook 'variable-pitch-mode)
#+END_SRC

*** Font Switching

Establish function to set font to variable width in current buffer:

#+BEGIN_SRC emacs-lisp
  (defun owl/buffer-face-mode-variable ()
    "Set font to a variable width (proportional) fonts in current buffer"
    (interactive)
    (setq buffer-face-mode-face '(:family "Input Serif"))
    (buffer-face-mode))
#+END_SRC

Establish function to set font to fixed width in current buffer:

#+BEGIN_SRC emacs-lisp
  (defun owl/buffer-face-mode-fixed ()
    "Sets a fixed width (monospace) font in current buffer"
    (interactive)
    (setq buffer-face-mode-face '(:family "Input Mono"))
    (buffer-face-mode))
#+END_SRC

Finally, establish a function to set font to a pretty writing font if Input Serif isn’t what I want. Currently, I’m using ET Book, inspired by Edward Tufte. This function works best in cases where I am not creating tables or doing coding, just hard-core prose.

#+BEGIN_SRC emacs-lisp
  (defun owl/buffer-face-mode-writing ()
    "Set font to a variable width (proportional) fonts in current buffer when writing prose"
    (interactive)
    (setq buffer-face-mode-face '(:family "ETBembo"))
    (buffer-face-mode))
#+END_SRC

=Control-c + u + f/v/w= to change font type:

#+BEGIN_SRC emacs-lisp
  (bind-keys ("C-c u f" . owl/buffer-face-mode-fixed)
             ("C-c u v" . owl/buffer-face-mode-variable)
             ("C-c u w" . owl/buffer-face-mode-writing))
#+END_SRC

** Themes

Ensure that variable pitches are respected in loaded themes:

#+BEGIN_SRC emacs-lisp
  (add-hook 'text-mode-hook
            (lambda ()
              (variable-pitch-mode 1)))
#+END_SRC

*** Themes in Rotation

**** [[https://peach-melpa.org/themes/flatui-dark-theme][flatui-dark-theme]]

#+BEGIN_SRC emacs-lisp
  (use-package flatui-dark-theme) 
#+END_SRC

**** [[https://peach-melpa.org/themes/doom-themes][doom-themes]]

=doom-themes-org-config= corrects and improves Org's native fontification.

#+BEGIN_SRC emacs-lisp
  (use-package doom-themes
    :config (setq doom-themes-enable-bold t
                  doom-themes-enable-italic t)
    (doom-themes-org-config))
#+END_SRC

**** [[https://peach-melpa.org/themes/kaolin-themes][kaolin-themes]]

#+begin_src emacs-lisp
  (use-package kaolin-themes)
#+end_src

**** [[https://peach-melpa.org/themes/poet-theme][poet-themes]]

For writing.

#+begin_src emacs-lisp
  (use-package poet-theme)
#+end_src

*** Functions to Switch Among Themes

Establish a rotating index of themes:

#+BEGIN_SRC emacs-lisp
  (setq owl/themes '(doom-opera  poet poet-dark poet-monochrome poet-dark-monochrome kaolin-aurora kaolin-valley-light kaolin-valley-dark  leuven))
  (setq owl/themes-index 0)
#+END_SRC

I then define a function to cycle through the index of themes and assign it to =<f12>= globally.

#+BEGIN_SRC emacs-lisp
  ;; Function to cycle through themes
  (defun owl/cycle-theme ()
    (interactive)
    (setq owl/themes-index (% (1+ owl/themes-index) (length owl/themes)))
    (owl/load-indexed-theme))

  (global-set-key (kbd "<f12>") 'owl/cycle-theme)
#+END_SRC

In order to prevent overlay, =owl/cycle-theme= depends on on two additional functions:

#+BEGIN_SRC emacs-lisp
  (defun owl/load-indexed-theme ()
    (owl/try-load-theme (nth owl/themes-index owl/themes)))

  (defun owl/try-load-theme (theme)
    (if (ignore-errors (load-theme theme :no-confirm))
	(mapcar #'disable-theme (remove theme custom-enabled-themes))
      (message "Unable to find theme file for ‘%s’" theme)))
#+END_SRC

** Accessibility
** Navigation
** Project Management

*** Version Control

**** Magit

Magit allows use of Git inside of Org-Mode. No more bumping out to a terminal.

#+begin_src emacs-lisp
  (use-package magit
    :preface (message "Magit is available at byte-compile time. I am proof thereof.")
    :commands magit-get-top-dir
    :init (setq magit-completing-read-function 'ivy-completing-read)
    :bind (("<f5>" . magit-status)
           ("C-c v t" . magit-status)
           ("C-c f" . magit-grep)))

#+end_src

** Org-Mode Initialization

I favor the newer version of Org-Mode over the built-in one(s).

#+begin_src emacs-lisp
  (use-package org
    :ensure org-plus-contrib
    :pin org
    :defer t)
#+end_src

I want to ensure that ELPA Org is prioritized above built-in Org:

#+begin_src emacs-lisp
  (require 'cl)
  (setq load-path (remove-if (lambda (x) (string-match-p "org$" x)) load-path))
#+end_src

Bleeding-edge Org (9.2, sometime in 2018) got rid of easy-tempalate expansion (e.g., =<s TAB= expands to a source block). Instead =org-tempo= must now be required if the old expansion method should still be used. Note that =C-c C-,= will pull up a list of expansion templates.

#+begin_src emacs-lisp
(require  'org-tempo)
#+end_src

Source blocks in =org-mode= should be sensitive to indentation norms:

#+begin_src emacs-lisp
  (setq org-src-tab-acts-natively t)
#+end_src

The default =org-directory= is =~/org=. It can be symlinked but needs to be present as such.

#+begin_src emacs-lisp
  (setq org-directory "~/org")
#+end_src

*** Org Modules

Org modules seem to be a bit like an internal extension system of self-contained libraries, with some modules comprising Org's core and others external to it. There are a number of Org extensions that are initialized as =org-modules=.
- The package/library/module =org-protocol= allows for capturing webpages and links while browsing and importing that into predefined capture templates.
- The module =org-inlinetask= allows for tasks to be inserted within outline structures without counting themselves as a heading of the structure /per se/. This is accomplished by making the task having $>15$ stars by default (something that can be changed globally or specific to a file).

Org-modules' individual behavior is established under their own headings below (if needed).

#+BEGIN_SRC emacs-lisp
  (setq org-modules (quote (org-bbdb
                            org-bibtex
                            org-crypt
                            org-gnus
                            org-id
                            org-info
                            org-habit
                            org-inlinetask
                            org-irc
                            org-protocol
                            org-w3m)))
#+END_SRC

**** COMMENT List of modules I'm considering not using:
- =org-gnus=
- =org-irc=
- =org-w3m=

** Org Agenda

#+begin_src emacs-lisp
  (bind-keys ("C-c a" . org-agenda))
#+end_src

#+begin_src emacs-lisp
  (org-agenda-files (apply 'append
                           (mapcar
                            (lambda (directory)
                              (directory-files-recursively
                               directory org-agenda-file-regexp))
                            '("~/org/captures/"
                              "~/org/hub/"
                              "~/org/cpb/"
                              "~/org/daybook/"
                              "~/.emacs.d/"))))
#+end_src

** Org Journal as Task Daybook

The directory =~/org/daybook= holds a set of short diary files that together comprise a bullet-journal-esque daybook.

#+BEGIN_SRC emacs-lisp
  (use-package org-journal
    :defer t
    :custom
    (org-journal-dir "~/org/daybook/")                                                             ; location
    (org-journal-date-format "%A, %x")                                                             ; date name format
    (org-journal-carryover-items "TODO=\"TODO\"|TODO=\"STARTED\"|TODO=\"NEXT\"|TODO=\"WAITING\"")  ; carryover items
    (org-journal-enable-agenda-integration t)                                                      ; ensure entries are on the agenda
    )
  (org-journal-update-auto-mode-alist)
#+END_SRC

I also specify an =org-capture= template that inserts a =TODO= heading, timestamped in a way =org-journal= and =org-agenda= will recognize. This consists of a function, =owl/org-journal-find-location=, and an associated capture block.

#+BEGIN_EXAMPLE
(defun owl/org-journal-find-location ()
      ;; Open today's journal, but specify a non-nil prefix argument in order to
      ;; inhibit inserting the heading; org-capture will insert the heading.
      (org-journal-new-entry t)
      ;; Position point on the journal's top-level heading so that org-capture
      ;; will add the new entry as a child entry.
      (goto-char (point-min)))
#+END_EXAMPLE

The capture block prompts for a task title, tag, and effort estimate; the tag is drawn in tags in the completion file and defaults to what I want, namely, the set of GTD context tags.

#+BEGIN_EXAMPLE
(defvar owl/org-basic-daybook-template
      "* TODO [#B] %(format-time-string org-journal-time-format)%^{Task} %^g\nSCHEDULED: %^t\n:PROPERTIES:\n:Created: %U\n:Effort: %^{effort|1:00|0:05|0:15|0:30|2:00|4:00}\n:END:"
      "Template for basic daybook task entry.")
#+END_EXAMPLE

#+BEGIN_EXAMPLE
  ("d"
     "daybook entry"
     entry
     (function owl/org-journal-find-location),
     owl/org-basic-daybook-template     ; template
     :empty-lines 1)
#+END_EXAMPLE

** Org Capture and Refile

Org-mode capture templates allow for central implementations of the PARA/BASB concept, as envisioned by Tiago Forte.

*** Capture Code (=org-capture=)

#+BEGIN_SRC emacs-lisp
  (use-package org-capture
    :ensure nil
    :after org
    :bind (("C-c c" . org-capture)
           ("C-c w" . org-refile))
    :preface
    (defvar owl/org-basic-task-template
      "* TODO [#B] %^{Task} %^g\nSCHEDULED: %^t\n:PROPERTIES:\n:Created: %U\n:Effort: %^{effort|1:00|0:05|0:15|0:30|2:00|4:00}\n:END:"
      "Template for basic task.")

    (defvar owl/org-daybook-task-template
      "* TODO %^{Task} %^g\n%^T\n:PROPERTIES:\n:Created: %U\n:Effort: %^{effort|1:00|0:05|0:15|0:30|2:00|4:00}\n:END:"
      "Template for basic daybook task entry. No time indicator for time of entry; prompt for scheduled time/date.")

    (defvar owl/org-daybook-entry-template
      "* NOTE %(format-time-string org-journal-time-format)%^{Title} %^g\n:PROPERTIES:\n:Created: %U\n:END:\n%?"
      "Template for basic daybook journal entry.")

    (defvar owl/org-contacts-template "* %(org-contacts-template-name)
                        :PROPERTIES:
                        :ADDRESS: %^{289 Cleveland St. Brooklyn, 11206 NY, USA}
                        :BIRTHDAY: %^{yyyy-mm-dd}
                        :EMAIL: %(org-contacts-template-email)
                        :NOTE: %^{NOTE}
                        :END:" "Template for org-contacts.")

    (defun owl/org-journal-find-location ()
      ;; Open today's journal, but specify a non-nil prefix argument in order to
      ;; inhibit inserting the heading; org-capture will insert the heading.
      (org-journal-new-entry t)
      ;; Position point on the journal's top-level heading so that org-capture
      ;; will add the new entry as a child entry.
      (goto-char (point-min)))

    (setq org-capture-templates `(("u"                             ; key
                                   "unscheduled task in place"     ; description
                                   entry                           ; type
                                   (function (lambda () (message "You have already arrived at your destination."))) ; target
                                   "* TODO [#B] %^{Todo} %^g\n:PROPERTIES:\n:Created: %U\n:Effort: %^{effort|1:00|0:05|0:15|0:30|2:00|4:00}\n:END:"                          ; template
                                   :prepend nil                    ; properties
                                   :empty-lines 0                  ; properties
                                   :created t                      ; properties
                                   )
                                  ("d" "daybook")
                                  ("dt"				
                                   "daybook task"
                                   entry
                                   (function owl/org-journal-find-location),
                                   owl/org-daybook-task-template     ; template
                                   :empty-lines 1)
                                  ("de"
                                   "daybook entry"
                                   entry
                                   (function owl/org-journal-find-location),
                                   owl/org-daybook-entry-template
                                   :empty-lines 1)
                                  ("a"               
                                   "article"         
                                   entry             
                                   (file+headline "~/org/cpb/bibliographies/mainref.org" "Article") 
                                   "* %^{Title} %(org-set-tags)  :article: \n:PROPERTIES:\n:Created: %U\n:Linked: %A\n:END:\n%i\nBrief description:\n%?" 
                                   :prepend t       
                                   :empty-lines 1   
                                   :created t       
                                   )
                                  ("c" 
                                   "contact"
                                   entry
                                   (file "~/org/hub/contacts.org"),
                                   owl/org-contacts-template
                                   :empty-lines 1)
                                  ("n"
                                   "note"
                                   entry
                                   (file+headline ,org-default-notes-file "Notes")
                                   "* %? :NOTE:\n:LOGBOOK:\n:CREATED: %U\n:END:"
                                   :prepend nil
                                   :empty-lines 0
                                   :created t
                                   )
                                  ("r"
                                   "recipe"
                                   entry
                                   (file+headline "~/org/cpb/food.org" "Recipes")
                                   "* TOCOOK %?\n:LOGBOOK:\n:CREATED: %U\n:END:\n:PROPERTIES:\n:SOURCE: \n:SERVES: \n:END:\n** Ingredients\n** Preparation"
                                   :prepend nil
                                   :empty-lines 0
                                   :created t
                                   )				
                                  ("e"
                                   "elfeed"
                                   entry
                                   (file "~/org/captures/captures.org")
                                   "* %a  %^G \n:LOGBOOK:\n:CAPTURED: %U\n:END:\n#+BEGIN_QUOTE\n%i\n#+END_QUOTE\n" 
                                   :prepend nil
                                   :empty-lines 0
                                   :created t
                                   )
                                  ("f" "Fitness")
                                  ("fw" "Weight" 
                                   table-line
                                   "| | %U | %^{Weight} | %^{Comment}"
                                   :immediate-finish t)
                                  ("s"
                                   "selected org-protocol link"
                                   entry
                                   (file "~/org/captures/captures.org")
                                   "* %^{Title}\nSource: %u, [[%:link][%:description]] \n #+BEGIN_QUOTE\n%i\n#+END_QUOTE\n\n\n%?")
                                  ("p" 
                                   "org-protocol-link"
                                   entry
                                   (file "~/org/captures/captures.org")
                                   "* %? [[%:link][%:description]] \nCaptured On: %U")				
                                  )
          )
    )
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun owl/org-call-src-block (name)
    ;; Based on <http://kitchingroup.cheme.cmu.edu/blog/2014/08/11/Using-org-mode-outside-of-Emacs-sort-of/>
    ;; This works better than the org-sbe (aka sbe) macro, because it
    ;; calls the block upon expansion, making it difficult to bind to
    ;; a command to run later
    ;; TODO: Use `org-babel-goto-named-src-block'!  I guess it's new...or not, it's from 2010!
    (org-with-wide-buffer
     (-when-let (src (org-element-map (org-element-parse-buffer) 'src-block
                       (lambda (element)
                         (when (string= name (org-element-property :name element))
                           element))
                       nil ;info
                       t ))
       (goto-char (org-element-property :begin src))
       (let ((org-confirm-babel-evaluate nil))
         (org-babel-execute-src-block)))))
#+END_SRC


*** Setting some Refile behaviors

I like =org-refile= to be promiscuous. To that end, it should use outline paths, complete in steps, and allow for the creation of parent nodes on-the-fly. See See https://blog.aaronbieber.com/2017/03/19/organizing-notes-with-refile.html for details on some of the hacks here.

I don't like the huge list of refile targets.

#+BEGIN_SRC emacs-lisp
  (setq org-refile-use-outline-path 'file)
  (setq org-outline-path-complete-in-steps nil)

  ;; Allow on-the-fly creation of parent headings
  (setq org-refile-allow-creating-parent-nodes 'confirm)
#+END_SRC


*** Refile targets: Integration with P.A.R.A.

All "areas" are included in the =~/org= directory itself. Soft-linking to a separate =~/areas= folder can result in disasters for /certain/ cloud-based repositories I'm currently using (perhaps not forever).

#+BEGIN_SRC emacs-lisp
  (setq org-refile-targets '((org-agenda-files :maxlevel . 8)
                             ("~/org/notebook/notebook.org" :maxlevel . 5)))

#+END_SRC

The package [[https://github.com/mwfogleman/org-randomnote][org-randomnote]], created by [[https://github.com/mwfogleman][Tasshin Fogleman]], further implements P.A.R.A. workflow by mimicking the “Random Note” functionality advocated by Tiago Forte with Evernote.

It can be used to “revisit nearly-forgotten but potentially-useful notes, or to spark creative insights by bringing older subjects into mind with new subjects.”

Currently, I bind it to =C-c g= and focus only on the =captures.org= file and several other well-stocked files. There is additional code that can traverse entire directories. I might want to add this later.

#+BEGIN_SRC emacs-lisp
  (use-package org-randomnote
    :ensure t
    :bind ("C-c g" . org-randomnote)
    :config (setq org-randomnote-candidates '("~/org/captures/captures.org"
                                              "~/org/hub/systems.org"
                                              "~/org/cpb/journal.org"
                                              "~/org/cpb/cs.org"
                                              "~/org/cpb/reading.org"
                                              "~/org/cpb/notebook.org"))
    )
#+END_SRC

** Clock
** Contacts Management
** Encryption
** Programming Languages Management

I use =org-babel= to embed and process some code in my Org files. Here I initialize the set of languages I commonly and less-than-commonly rely on:

#+begin_src emacs-lisp
  (use-package gnuplot)
#+end_src

#+begin_src emacs-lisp
  (org-babel-do-load-languages
   'org-babel-load-languages '((C . t)
                               (shell . t)
                               (python . t)
                               (gnuplot . t)))
#+end_src

** Reference and Resource Management
** Publishing
** Writing Environment

*** Distraction-Free Writing

I use the =writeroom-mode= package as my main distraction destroyer. It creates a fullscreen Emacs environment, removes the modeline, and centers the text. It works well in its default aspect for me, though it's quite configurable.

I bind it to =C-<f11>= to keep it close to the toggle for =poet-theme=, which I made =C-<f12>=.

#+BEGIN_SRC emacs-lisp
  (use-package writeroom-mode
    :bind ("C-<f11>" . writeroom-mode)
    :config
    (setq writeroom-width 100
          writeroom-mode-line nil
          writeroom-global-effects '(writeroom-set-bottom-divider-width
                                     writeroom-set-internal-border-width
                                     (lambda (arg)
                                       (let ((langs '("python"
                                                      "emacs-lisp"
                                                      "common-lisp"
                                                      "js"
                                                      "ruby")))
                                         (cond
                                          ((= arg 1)
                                           (progn
                                             (setq org-src-block-faces
                                                   (mapcar (lambda (lang) (list lang '(:family "Input Mono" :height 1.0))) langs))
                                             (normal-mode)
                                             (variable-pitch-mode)))
                                          ((= arg -1)
                                           (progn
                                             (setq org-src-block-faces
                                                   (mapcar (lambda (lang) (list lang '(:family "Input Mono" :height 1.0))) langs))
                                             (normal-mode)
                                             (variable-pitch-mode)
                                             (variable-pitch-mode)))))))))
#+END_SRC

** Dashboard
** Toc-org
 
Install and load the =toc-org= package after org mode is loaded. This is the package that automatically generates an up to date table of contents for us.

 #+begin_src emacs-lisp
   (use-package toc-org
     :after org
     :init (add-hook 'org-mode-hook #'toc-org-enable))
 #+end_src

** Post Initialization

 Lower our GC thresholds back down to a sane level.

 #+begin_src emacs-lisp
   (setq gc-cons-threshold 16777216
	 gc-cons-percentage 0.1)
 #+end_src
